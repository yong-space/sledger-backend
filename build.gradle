plugins {
    id 'org.springframework.boot' version '3.2.1'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'io.freefair.lombok' version '8.4'
    id 'java'
    id 'jacoco'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

repositories { mavenCentral() }

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.add("-parameters")
}

def testContainersVersion = '1.19.3'

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-data-mongodb"
    implementation "org.springframework.boot:spring-boot-starter-mail"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.bouncycastle:bcprov-jdk18on:1.77"
    implementation "com.auth0:java-jwt:4.4.0"
    implementation ("com.opencsv:opencsv:5.9") {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    implementation ("org.jxls:jxls-jexcel:1.0.9") {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'log4j', module: 'log4j'
    }
    developmentOnly "org.springframework.boot:spring-boot-devtools"

    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
    testImplementation "org.junit.jupiter:junit-jupiter:5.10.1"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.security:spring-security-test"
    testImplementation "org.testcontainers:testcontainers:$testContainersVersion"
    testImplementation "org.testcontainers:junit-jupiter:$testContainersVersion"
    testImplementation "org.testcontainers:mongodb:$testContainersVersion"
    testImplementation "com.icegreen:greenmail-junit5:2.0.1"
}

tasks.named("jar") {enabled = false }

tasks.withType(Test).configureEach { useJUnitPlatform() }
test {
    reports {
        junitXml.required = false
        html.required = false
    }
    finalizedBy jacocoTestReport
    jacoco { excludes = [ 'tech.sledger.model.*' ] }
}
jacocoTestReport {
    dependsOn test
    afterEvaluate {
        getClassDirectories().setFrom(classDirectories.files.collect {
            fileTree(dir: it, exclude: [ 'tech/sledger/*.class', 'tech/sledger/model/**' ])
        })
    }
    reports {
        xml.required = true
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}
